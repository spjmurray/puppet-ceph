# == Define: ceph::keyring
#
# Installs a keyring to the specified location. If set on
# a monitor node the key will be injected into the auth
# database
#
# === Parameters
#
# [*name*]
#   User the key belongs to e.g. client.admin
#
# [*filename*]
#   Absolute path of the keyring e.g. /etc/ceph/ceph.client.admin.keyring'
#
# [*key*]
#   Key as generated by ceph-authtool (16 bytes, base 64 encoding)
#
# [*caps_mon*]
#   Monitor capabilities
#
# [*caps_mds*]
#   Metadata server capabilities
#
# [*caps_osd*]
#   Object storage daemon capabilities
#
# [*caps_mgr*]
#   Manager capabilities
#
# [*owner*]
#   Keyring file owner
#
# [*group*]
#   Keyring file group
#
# [*mode*]
#   Keyring file mode
#
define ceph::keyring (
  $filename = undef,
  $key,
  $caps_mon = undef,
  $caps_mds = undef,
  $caps_osd = undef,
  $caps_mgr = undef,
  $owner = $::ceph::user,
  $group = $::ceph::group,
  $mode = '0644',
) {

  assert_private()


  if $filename {
    # Note: puppet appears to run matchpathcon before ceph is installed and breaks idempotency
    if $filename =~ /^\/var\/lib\/ceph/ {
      File {
        seltype => $::ceph::seltype,
      }
    }

    file { $filename:
      ensure  => file,
      owner   => $owner,
      group   => $group,
      mode    => $mode,
      content => template('ceph/keyring.erb'),
    }
  }

  if $ceph::mon {

    include ::ceph::mon

    $mon_name = 'mon.'
    $mon_key = "/var/lib/ceph/mon/ceph-${ceph::mon_id}/keyring"

    exec { "keyring inject ${name}":
      command => join(["/bin/sh -c 'echo -e [${name}]\\\\nkey=${key} | /usr/bin/ceph -n ${mon_name} -k ${mon_key} auth add ${name} -i -",
                       $caps_mon?{undef=>"",default=>" mon \"${caps_mon}\""},
                       $caps_rgw?{undef=>"",default=>" rgw \"${caps_rgw}\""},
                       $caps_osd?{undef=>"",default=>" osd \"${caps_osd}\""},
                       $caps_mds?{undef=>"",default=>" mds \"${caps_mds}\""},
                       $caps_mgr?{undef=>"",default=>" mgr \"${caps_mgr}\""},
                       "'"]),
      unless  => "/usr/bin/ceph -n ${mon_name} -k ${mon_key} auth list | grep ${name}",
    }

  }

}
